<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>spr_marker_event</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>-5</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///The titlescreen menu

//Play the title theme
audio_play_sound(bgm_title, 0, true);

//Menu mode
//1: Activating...
//2: Active
//3: Key
mode = 1;
alarm[0] = 30

//Page
page = 0;

//Item
current = 1;

//Save File info
for (i=0; i&lt;3; i++) {

    coins[i] = 0;
    complete[i] = 0;
}

//Menu 0: Main
menu[0,0] = 3;
menu[0,1] = "Start Game"
menu[0,2] = "Options"
menu[0,3] = "Exit"

//Menu 1: File Selection
menu[1,0] = 4;
menu[1,1] = "File A - New!";
menu[1,2] = "File B - New!";
menu[1,3] = "File C - New!";
menu[1,4] = "Back";

//Menu 2: Options
menu[2,0] = 7;
menu[2,1] = "Keyboard Configuration";
menu[2,2] = "Reserve Auto-Drop: On";
menu[2,3] = "Set Window Size";
menu[2,4] = "V-Sync: On";
menu[2,5] = "FPS: On";
menu[2,6] = "View Credits";
menu[2,7] = "Back";

//Menu 3: Keys 1 / 2
menu[3,0] = 7;
menu[3,1] = "Run, Shoot: " + string(key_to_string(global.controlkey));
menu[3,2] = "Jump, Spin: " + string(key_to_string(global.shiftkey));
menu[3,3] = "Reserve   : " + string(key_to_string(global.spacekey));
menu[3,4] = "Pause     : " + string(key_to_string(global.enterkey));
menu[3,5] = "Reset all keys"
menu[3,6] = "Next"
menu[3,7] = "Back"

//Menu 4: Keys 2 / 2
menu[4,0] = 7;
menu[4,1] = "Up        : " + string(key_to_string(global.upkey));
menu[4,2] = "Down      : " + string(key_to_string(global.downkey));
menu[4,3] = "Left      : " + string(key_to_string(global.leftkey));
menu[4,4] = "Right     : " + string(key_to_string(global.rightkey));
menu[4,5] = "Reset all keys"
menu[4,6] = "Previous";
menu[4,7] = "Back";
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="1">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Reset all keys

//Control key
global.controlkey = vk_control;

//Shift key
global.shiftkey = vk_shift;

//Space key
global.spacekey = vk_space;

//Enter key
global.enterkey = vk_enter;

//Up key
global.upkey = vk_up;

//Down key
global.downkey = vk_down;

//Left key
global.leftkey = vk_left;

//Right key
global.rightkey = vk_right;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Set active mode

mode = 2;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="2">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Update save file info

//If 'File A' exists, open it
if (file_exists("GMEAInfoA.ini")) {

    //Check data
    ini_open("GMEAInfoA.ini");
    coins[0] = ini_read_real("Coins", "Data", 0);
    exits[0] = ini_read_real("Exits", "Data", 0);
    completion[0] = ini_read_real("Clear", "Completion", 0);
    ini_close();
    
    //Print it
    menu[1,1] = "File A ..... E: " + string_format(exits[0],3,0) + " - | " + string_add_zeros(coins[0], 3) + "/" + string_add_zeros(global.levelmax*3, 3) + " (" + string_format(completion[0], 3, 0) + "%)";
}

//Otherwise
else {

    menu[1,1] = "File A ..... New!";
}

//If 'File B' exists, open it
if (file_exists("GMEAInfoB.ini")) {

    //Check data
    ini_open("GMEAInfoB.ini");
    coins[1] = ini_read_real("Coins", "Data", 0);
    exits[1] = ini_read_real("Exits", "Data", 0);
    completion[1] = ini_read_real("Clear", "Completion", 0);
    ini_close();
    
    //Print it
    menu[1,2] = "File B ..... E: " + string_format(exits[1],3,0) + " - | " + string_add_zeros(coins[1], 3) + "/" + string_add_zeros(global.levelmax*3, 3) + " (" + string_format(completion[1], 3, 0) + "%)";
}

//Otherwise
else {

    menu[1,2] = "File B ..... New!";
}

//If 'File C' exists, open it
if (file_exists("GMEAInfoC.ini")) {

    //Check data
    ini_open("GMEAInfoC.ini");
    coins[2] = ini_read_real("Coins", "Data", 0);
    exits[2] = ini_read_real("Exits", "Data", 0);
    completion[2] = ini_read_real("Clear", "Completion", 0);
    ini_close();
    
    //Print it
    menu[1,3] = "File C ..... E: " + string_format(exits[2],3,0) + " - | " + string_add_zeros(coins[2], 3) + "/" + string_add_zeros(global.levelmax*3, 3) + " (" + string_format(completion[2], 3, 0) + "%)";
}

//Otherwise
else {

    menu[1,3] = "File C ..... New!";
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Manage keyboard presses

//If the menu is not on key change mode
if (mode == 2) {

    //If the player presses the 'Up' key
    if (keyboard_check_pressed(global.upkey)) {
    
        //Play 'Move' sound
        audio_play_sound(snd_move, 0, false);
    
        //Cycle between options
        if (current == 1)
            current = menu[page,0];
        else
            current--;
    }
    
    //Otherwise, if the player presses the 'Down' key
    else if (keyboard_check_pressed(global.downkey)) {
    
        //Play 'Move' sound
        audio_play_sound(snd_move, 0, false);
    
        //Cycle between options        
        if (current == menu[page,0])
            current = 1;
        else
            current++;
    }
    
    //Otherwise, if the player presses the 'Shift' key
    else if (keyboard_check_pressed(global.shiftkey)) {
    
        //Page 0: Main
        if (page == 0) {
        
            //Option 1: Start Game
            if (current == 1) {
                
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Go to page 1
                page = 1;
                
                //Reset option to 1
                current = 1;
            }
            
            //Option 2: Options
            else if (current == 2) {
            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Go to page 2
                page = 2;
                
                //Reset option to 1
                current = 1;
            }
            
            //Option 3: Exit
            else if (current == 3) {
            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Exit the game
                game_end();
            }
        }
        
        //Page 1: File selection
        else if (page == 1) {
        
            //Options 1 to 3: File selection
            if (current != 4) {
        
                //Stop stream if it's playing
                audio_stop_sound(bgm_title);
                
                //Check what file is selected
                switch (current) {
                
                    //File A
                    case (1): global.file = "GMEASaveA.sav"; break;
                    
                    //File B
                    case (2): global.file = "GMEASaveB.sav"; break;
                    
                    //File C
                    case (3): global.file = "GMEASaveC.sav"; break;
                }
                
                //If the file exists
                if (file_exists(global.file)) {
                
                    load(global.file);
                    if (instance_number(obj_curtainout) == 0) {
                    
                        audio_play_sound(snd_enterstage, 0, false);     
                        with (instance_create(view_xview[0]+global.gw/2,view_yview[0]+global.gh/2,obj_curtainout))                        
                            target = rm_map;                        
                    }
                }
                
                //Otherwise, go to the intro screen
                else {

                    if (instance_number(obj_curtainout) == 0) {
                    
                        audio_play_sound(snd_enterstage, 0, false);     
                        with (instance_create(view_xview[0]+global.gw/2,view_yview[0]+global.gh/2,obj_curtainout))
                            target = rm_intro;
                    }                    
                }
            }
            
            //Choice 3: Back
            else if (current == 4) {
                                
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
            
                //Go to page 0
                page = 0;
                
                //Set the choice
                current = 1;       
            }            
        }
        
        //Page 2: Options
        else if (page == 2) {
        
            //Option 1: Keyboard configuration
            if (current == 1) {
            
                //Check if a gamepad is connected
                if (global.gamepad == false) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Go to page 3
                    page = 3;
                    
                    //Set the choice
                    current = 1;
                }
                
                //Otherwise, play 'Wrong' sound
                else
                    audio_play_sound(snd_wrong, 0, false);
            }
            
            //Option 2: Reserve Auto-Drop
            else if (current == 2) {
            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Enable Reserve item auto-drop
                global.reservedrop = !global.reservedrop;
            }
            
            //Option 3: Window size
            else if (current == 3) {
            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
            
                //Switch filter
                with (obj_persistent) {
                
                    //If the window is not set to fullscreen
                    if (fullscreen == false) {
                
                        //Change filter
                        filter++;
                        
                        //Check if the filter is the second one
                        if (filter == 3) {
                        
                            //Set default filter
                            filter = 0;
                            
                            //Set fullscreen variable
                            fullscreen = true;
                            
                            //Set fullscreen mode
                            window_set_fullscreen(true);                                                             
                        }
                    }
                    
                    //Otherwise, set it to windowed mode
                    else {
                    
                        //Change fullscreen variable
                        fullscreen = false;
                    
                        //Set windowed mode
                        window_set_fullscreen(false);             
                    }
                    
                    //Apply settings
                    event_user(0);   
                }                    
            }
            
            //Option 4: V-Sync
            else if (current == 4) {
            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Enable V-Sync
                with (obj_persistent) {
                
                    //Toggle vertical syncronization
                    vsync = !vsync;
                    
                    //Apply settings
                    event_user(0);
                }
            }
            
            //Option 5: FPS
            else if (current == 5) {
            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Enable FPS on screen
                with (obj_persistent) {
                
                    //Toggle FPS
                    showfps = !showfps;
                }
            }
            
            //Option 6: Credits
            else if (current == 6) {
            
                //Stop stream if it's playing
                audio_stop_sound(bgm_title);
            
                //Go to credits room
                if (instance_number(obj_curtainout) == 0) {
                
                    audio_play_sound(snd_enterstage, 0, false);     
                    with (instance_create(view_xview[0]+global.gw/2,view_yview[0]+global.gh/2,obj_curtainout))
                        target = rm_credits;
                }                              
            }
            
            //Option 7: Back
            else if (current == 7) {
                            
                //Play 'Correct' sound
                audio_play_sound(snd_correct, 0, false);
                
                //Go to page 3
                page = 0;
                
                //Set the choice
                current = 1;                    
            }
        }
        
        //Page 3: Keys 1/2
        else if (page == 3) {
        
            //Exit if the gamepad is connected
            if (global.gamepad == true) {
                
                //Go to page 2
                page = 2;
                
                //Set the choice
                current = 1;            
            }
            
            //Otherwise
            else {
        
                //Options 1 to 4
                if (current &lt; 5) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Set 'Key' mode
                    mode = 3;
                    
                    //Switch current
                    switch (current) {
                    
                        case (1): set_keys(0, "Press a key...", vk_control); break;
                        case (2): set_keys(1, "Press a key...", vk_shift); break;
                        case (3): set_keys(2, "Press a key...", vk_space); break;
                        case (4): set_keys(3, "Press a key...", vk_enter); break;
                    }                    
                    
                    //Set 'Active' mode
                    alarm[0] = 3;
                }
            
                //Option 5: Reset keys
                else if (current == 5) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Reset all keys
                    alarm[1] = 1;
                }
                
                //Option 6: Page 2 / 2
                else if (current == 6) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Go to page 4
                    page = 4;
                }
                
                //Option 7: Back
                else if (current == 7) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Go to page 2
                    page = 2;
                    
                    //Set the choice
                    current = 1;
                }
            }
        }
        
        //Page 2: Keys 2/2
        else if (page == 4) {
                
            //Exit if the gamepad is connected
            if (global.gamepad == true) {
                
                //Go to page 2
                page = 2;
                
                //Set the choice
                current = 1;            
            }
            
            //Otherwise
            else {
        
                //Options 1 to 4
                if (current &lt; 5) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Set 'Key' mode
                    mode = 3;
                    
                    //Switch current
                    switch (current) {
                    
                        case (1): set_keys(4, "Press a key...", vk_up); break;
                        case (2): set_keys(5, "Press a key...", vk_down); break;
                        case (3): set_keys(6, "Press a key...", vk_left); break;
                        case (4): set_keys(7, "Press a key...", vk_right); break;
                    }                    
                    
                    //Set 'Active' mode
                    alarm[0] = 3;
                }
            
                //Option 5: Reset keys
                else if (current == 5) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Reset all keys
                    alarm[1] = 1;
                }
                
                //Option 6: Page 2 / 2
                else if (current == 6) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Go to page 4
                    page = 3;
                }
                                
                //Option 7: Back
                else if (current == 7) {
                
                    //Play 'Correct' sound
                    audio_play_sound(snd_correct, 0, false);
                    
                    //Go to page 2
                    page = 2;
                    
                    //Set the choice
                    current = 1;
                }
            }
        }
    }
}

//Update Reserve Auto-Drop text
if (global.reservedrop)
    menu[2,2] = "Reserve Auto-Drop: On";
else
    menu[2,2] = "Reserve Auto-Drop: Off";

//Update V-Sync text
if (obj_persistent.vsync)
    menu[2,4] = "V-Sync: On";
else
    menu[2,4] = "V-Sync: Off";
    
//Update FPS text
if (obj_persistent.showfps)
    menu[2,5] = "FPS: On";
else
    menu[2,5] = "FPS: Off";
    
//Update Key Text
menu[3,1] = "Run, Shoot: " + string(key_to_string(global.controlkey));
menu[3,2] = "Jump, Spin: " + string(key_to_string(global.shiftkey));
menu[3,3] = "Reserve   : " + string(key_to_string(global.spacekey));
menu[3,4] = "Pause     : " + string(key_to_string(global.enterkey));
menu[4,1] = "Up        : " + string(key_to_string(global.upkey));
menu[4,2] = "Down      : " + string(key_to_string(global.downkey));
menu[4,3] = "Left      : " + string(key_to_string(global.leftkey));
menu[4,4] = "Right     : " + string(key_to_string(global.rightkey));


</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="4">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Create curtain out effect

instance_create(0,0,obj_curtainin);
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>///Draw the menu.

//Display the menu
if (mode != 0) {

    //Set up the font
    draw_set_font(global.font_interface);
    
    //Set the alpha
    draw_set_alpha(1);
    
    //Align the text to the top
    draw_set_valign(fa_left);
    
    //Get the length of the menu
    for (var i=1; i&lt;=menu[page,0]; i++) {
        
        //Draw the text...
        if (i == current)
            draw_text_colour_shadowed(view_xview[0]+26, view_yview[0]+120+(12*(i-1)),menu[page,i], c_black, c_white, 1, 1, 0.5, 1);
        else
            draw_text_colour_shadowed(view_xview[0]+26, view_yview[0]+120+(12*(i-1)),menu[page,i], c_black, c_ltgray, 1, 1, 0.5, 1);
        
        //...and draw a arrow next to the selected option 
        if (i == current)
            draw_sprite_ext(spr_cursor, -1, view_xview[0]+10, view_yview[0]+120+(12*(i-1)), 1, 1, 0, c_white, 1);
    }
    
    //Reset text alignment
    draw_set_valign(fa_top);
}

//Align the text
draw_set_halign(fa_right);

//Set up the font
draw_set_font(global.font_interface);  

//Set the alpha
draw_set_alpha(1);

//Draw the text
draw_text_colour_shadowed(view_xview[0]+global.gw-4, view_yview[0]+4, string(cs_version), c_black, c_white, 1, 1, 0.5, 1);
draw_text_colour_shadowed(view_xview[0]+global.gw-4, view_yview[0]+global.gh-12,"2008-2020 GMETEAM / GREEN BARON GAMES", c_black, c_white, 1, 1, 0.5, 1);

//Re align the text
draw_set_halign(fa_left);
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
